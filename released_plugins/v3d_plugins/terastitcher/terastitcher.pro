#last change by PHC, 20130722 to make it successful for static link

#terastitcher plugin project file
TEMPLATE	= lib
CONFIG	+= qt plugin warn_off
#CONFIG += x86_64
CONFIG += use_static_libs


#set up Vaa3D and Qt source path
win32{
    V3DMAINPATH =  ../../../../v3d_external/v3d_main
}
else{
    V3DMAINPATH =  ../../../v3d_main
}
QT_PATH = $$dirname(QMAKE_QMAKE)/..


#set up third party libraries under MacOS and Linux
use_static_libs{
        INCLUDEPATH += ../teramanager/include/opencv
        INCLUDEPATH += ../teramanager/include
    mac{
        LIBS += -L../teramanager/lib/opencv/mac_x86_64
        LIBS += -L../teramanager/lib/opencv/mac_x86_64/3rdparty
    }
    unix:!mac{
        LIBS += -L../teramanager/lib/opencv/unix_x86_64
        LIBS += -L../teramanager/lib/opencv/unix_x86_64/3rdparty
    }
    win32{
        #WARNING! These precompiled libraries must match with your VC (vc9, vc10 or vc11) compiler!
        LIBS += -L"$$_PRO_FILE_PWD_/../teramanager/lib/opencv/win64/vc9"
        LIBS += -L"$$_PRO_FILE_PWD_/../teramanager/lib/opencv/win64/vc9/3rdparty"

        #on VC, OpenCV precompiled libraries have further dependencies
        LIBS+=-llibpng
        LIBS+=-ladvapi32
        LIBS+=-lcomctl32
        LIBS+=-luser32
        LIBS+=-lgdi32
    }
} else{
    #OpenCV headers and library folders
    INCLUDEPATH += /usr/local/include/opencv
    LIBS+= -L/usr/local/lib
}
LIBS+= -lopencv_core -lopencv_imgproc -lopencv_highgui \
 -lzlib \
 -llibtiff \
 -llibjpeg \
 -lIlmImf \
 -llibjasper 


#set up of the plugin under Windows requires special attention
win32{

    #the directory where files like "ui_template_matching_cellseg.h", "moc*.cxx" and similar were generated by CMake/QMake + Visual Studio
    V3DBUILDPATH = $$V3DMAINPATH/../../build_cmake_vc9/v3d_main
    INCLUDEPATH += $$V3DBUILDPATH/v3d
    INCLUDEPATH += $$V3DBUILDPATH/v3dbase

    #libtiff API
    INCLUDEPATH += $$V3DMAINPATH/common_lib/src_packages/tiff-4.0.1/libtiff

    #Vaa3D and OpenCV libraries are compiled using the multithread-DLL version of the run-time library
    CONFIG(debug, debug|release) {
        QMAKE_CXXFLAGS += /MDd
        QMAKE_CXXFLAGS += /NODEFAULTLIB:MSVCRT
    }
    CONFIG(release, debug|release) {
        QMAKE_CXXFLAGS += /MD
    }
}

#on MacOSX, undefined symbols referenced from OpenCV are ignored and checked dynamically at execution time
mac{
LIBS += -undefined dynamic_lookup
}


#Vaa3D headers and sources needed by the plugin
INCLUDEPATH+= $$QT_PATH/demos/shared
INCLUDEPATH += $$V3DMAINPATH/basic_c_fun
INCLUDEPATH += $$V3DMAINPATH/common_lib/include
SOURCES += $$V3DMAINPATH/basic_c_fun/v3d_message.cpp
SOURCES += $$V3DMAINPATH/basic_c_fun/stackutil.cpp
SOURCES += $$V3DMAINPATH/basic_c_fun/mg_image_lib.cpp
SOURCES += $$V3DMAINPATH/basic_c_fun/mg_utilities.cpp
SOURCES += $$V3DMAINPATH/basic_c_fun/basic_memory.cpp
SOURCES += $$V3DMAINPATH/basic_c_fun/basic_4dimage.cpp
#SOURCES += $$V3DMAINPATH/basic_c_fun/imageio_mylib.cpp
SOURCES += $$V3DMAINPATH/basic_c_fun/basic_surf_objs.cpp
#LIBS += -L. -lv3dtiff -L$$V3DMAINPATH/common_lib/lib \
            #-L$$V3DMAINPATH/common_lib/src_packages/mylib_tiff -lmylib

#set up TeraStitcher tool
RESOURCES += icons.qrc
DEFINES += _VAA3D_PLUGIN_MODE
INCLUDEPATH += ./include
INCLUDEPATH += ./src/core/IOManager
INCLUDEPATH += ./src/core/TinyXML
INCLUDEPATH += ./src/core/CrossMIPs
INCLUDEPATH += ./src/core/VolumeManager
INCLUDEPATH += ./src/core/ImageManager
INCLUDEPATH += ./src/core/Stitcher
HEADERS += ./src/core/TinyXML/tinyxml.h
HEADERS += ./src/core/TinyXML/tinystr.h
HEADERS += ./src/core/IOManager/MyException.h
HEADERS += ./src/core/IOManager/IOManager_defs.h
HEADERS += ./src/core/IOManager/IOManager.h
HEADERS += ./src/core/IOManager/ProgressBar.h
HEADERS += ./src/core/CrossMIPs/CrossMIPs.h
HEADERS += ./src/core/CrossMIPs/my_defs.h
HEADERS += ./src/core/CrossMIPs/compute_funcs.h
HEADERS += ./src/core/Stitcher/S_config.h
HEADERS += ./src/core/Stitcher/Displacement.h
HEADERS += ./src/core/Stitcher/DisplacementMIPNCC.h
HEADERS += ./src/core/Stitcher/PDAlgo.h
HEADERS += ./src/core/Stitcher/PDAlgoMIPNCC.h
HEADERS += ./src/core/Stitcher/StackRestorer.h
HEADERS += ./src/core/Stitcher/StackStitcher.h
HEADERS += ./src/core/Stitcher/TPAlgo.h
HEADERS += ./src/core/Stitcher/TPAlgoMST.h
HEADERS += ./src/core/VolumeManager/dirent_win.h
HEADERS += ./src/core/VolumeManager/vmBlock.h
HEADERS += ./src/core/VolumeManager/vmBlockVolume.h
HEADERS += ./src/core/VolumeManager/vmStack.h
HEADERS += ./src/core/VolumeManager/vmStackedVolume.h
HEADERS += ./src/core/VolumeManager/vmVirtualStack.h
HEADERS += ./src/core/VolumeManager/vmVirtualVolume.h
HEADERS += ./src/core/VolumeManager/VM_config.h
HEADERS += ./src/core/ImageManager/IM_config.h
HEADERS += ./src/core/ImageManager/Block.h
HEADERS += ./src/core/ImageManager/Stack.h
HEADERS += ./src/core/ImageManager/RawFmtMngr.h
HEADERS += ./src/core/ImageManager/Tiff3DMngr.h
HEADERS += ./src/core/ImageManager/SimpleVolume.h
HEADERS += ./src/core/ImageManager/SimpleVolumeRaw.h
HEADERS += ./src/core/ImageManager/RawVolume.h
HEADERS += ./src/core/ImageManager/StackedVolume.h
HEADERS += ./src/core/ImageManager/TiledVolume.h
HEADERS += ./src/core/ImageManager/TiledMCVolume.h
HEADERS += ./src/core/ImageManager/VirtualVolume.h
HEADERS += ./src/core/ImageManager/TimeSeries.h
HEADERS += ./src/control/CPlugin.h
HEADERS += ./src/control/CImport.h
HEADERS += ./src/control/CMergeTiles.h
HEADERS += ./src/control/CPreview.h
HEADERS += ./src/control/CDisplComp.h
HEADERS += ./src/presentation/PMain.h
HEADERS += ./src/presentation/PTabDisplComp.h
HEADERS += ./src/presentation/PTabDisplProj.h
HEADERS += ./src/presentation/PTabDisplThresh.h
HEADERS += ./src/presentation/PTabImport.h
HEADERS += ./src/presentation/PTabMergeTiles.h
HEADERS += ./src/presentation/PTabPlaceTiles.h
HEADERS += ./src/presentation/QHelpBox.h
HEADERS += ./src/presentation/QMyTabWidget.h
HEADERS += ./src/presentation/QPrefixSuffixLineEdit.h

SOURCES += ./src/core/TinyXML/tinyxml.cpp
SOURCES += ./src/core/TinyXML/tinystr.cpp
SOURCES += ./src/core/TinyXML/tinyxmlerror.cpp
SOURCES += ./src/core/TinyXML/tinyxmlparser.cpp
SOURCES += ./src/core/IOManager/IOManager.cpp
SOURCES += ./src/core/IOManager/ProgressBar.cpp
SOURCES += ./src/core/CrossMIPs/compute_funcs.cpp
SOURCES += ./src/core/CrossMIPs/libcrossmips.cpp
SOURCES += ./src/core/Stitcher/Displacement.cpp
SOURCES += ./src/core/Stitcher/DisplacementMIPNCC.cpp
SOURCES += ./src/core/Stitcher/PDAlgo.cpp
SOURCES += ./src/core/Stitcher/PDAlgoMIPNCC.cpp
SOURCES += ./src/core/Stitcher/StackRestorer.cpp
SOURCES += ./src/core/Stitcher/StackStitcher.cpp
SOURCES += ./src/core/Stitcher/TPAlgo.cpp
SOURCES += ./src/core/Stitcher/TPAlgoMST.cpp
SOURCES += ./src/core/VolumeManager/vmBlock.cpp
SOURCES += ./src/core/VolumeManager/vmBlockVolume.cpp
SOURCES += ./src/core/VolumeManager/vmStack.cpp
SOURCES += ./src/core/VolumeManager/vmStackedVolume.cpp
SOURCES += ./src/core/VolumeManager/vmVirtualStack.cpp
SOURCES += ./src/core/VolumeManager/vmVirtualVolume.cpp
SOURCES += ./src/core/ImageManager/IM_config.cpp
SOURCES += ./src/core/ImageManager/Block.cpp
SOURCES += ./src/core/ImageManager/Stack.cpp
SOURCES += ./src/core/ImageManager/RawFmtMngr.cpp
SOURCES += ./src/core/ImageManager/Tiff3DMngr.cpp
SOURCES += ./src/core/ImageManager/StackedVolume.cpp
SOURCES += ./src/core/ImageManager/TiledVolume.cpp
SOURCES += ./src/core/ImageManager/VirtualVolume.cpp
SOURCES += ./src/control/CPlugin.cpp
SOURCES += ./src/control/CImport.cpp
SOURCES += ./src/control/CMergeTiles.cpp
SOURCES += ./src/control/CPreview.cpp
SOURCES += ./src/control/CDisplComp.cpp
SOURCES += ./src/presentation/PMain.cpp
SOURCES += ./src/presentation/PTabDisplComp.cpp
SOURCES += ./src/presentation/PTabDisplProj.cpp
SOURCES += ./src/presentation/PTabDisplThresh.cpp
SOURCES += ./src/presentation/PTabImport.cpp
SOURCES += ./src/presentation/PTabMergeTiles.cpp
SOURCES += ./src/presentation/PTabPlaceTiles.cpp
SOURCES += ./src/presentation/QHelpBox.cpp
SOURCES += ./src/presentation/QPrefixSuffixLineEdit.cpp


#set up target
TARGET	= $$qtLibraryTarget(terastitcherplugin)
DESTDIR	= ../../../bin/plugins/image_stitching/terastitcher
#DESTDIR = ../../v3d/plugins/image_stitching/terastitcher


